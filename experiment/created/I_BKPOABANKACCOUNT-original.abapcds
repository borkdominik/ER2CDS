@EndUserText.label: 'Bank Accounts of POA'
@AbapCatalog.viewEnhancementCategory: [#NONE]
@AccessControl.authorizationCheck: #CHECK
@ObjectModel.usageType:{ serviceQuality: #C,
                         sizeCategory: #M,
                         dataClass: #MASTER }
@ObjectModel.supportedCapabilities:  [ #SQL_DATA_SOURCE,
                                       #CDS_MODELING_DATA_SOURCE,
                                       #CDS_MODELING_ASSOCIATION_TARGET ]
@VDM.viewType: #BASIC

define view entity I_BkPOABankAccount
  as select from fclmpoa_ba

  association [1..1] to I_BankPowerOfAttorney  as _BankPowerOfAttorney  on $projection.BankPowerOfAttorneyUUID = _BankPowerOfAttorney.BankPowerOfAttorneyUUID
  association [1..1] to I_BankAccount          as _BankAccount          on $projection.BankAccountInternalID = _BankAccount.BankAccountInternalID
  association [1..1] to I_BankAccountBasicData as _BankAccountBasicData on $projection.BankAccountInternalID = _BankAccountBasicData.BankAccountInternalID
  association [0..*] to I_BankAccountText      as _BankAccountText      on $projection.BankAccountInternalID = _BankAccountText.BankAccountInternalID
{
  key poa_uuid                                                                              as BankPowerOfAttorneyUUID,
  key poa_bank_account_uuid                                                                 as BkPOABankAccountUUID,
      acc_id                                                                                as BankAccountInternalID,
      assignment_tstmp_utc                                                                  as BkPOABkAcctAssignmentDateTime,
      removal_tstmp_utc                                                                     as BkPOABkAcctRemovalDateTime,
      created_by                                                                            as CreatedByUser,
      creation_tstmp_utc                                                                    as CreationDateTime,
      last_changed_by                                                                       as LastChangedByUser,
      local_last_changed_tstmp_utc                                                          as LocalLastChangeDateTime,
      _BankPowerOfAttorney,
      _BankAccount,
      _BankAccountBasicData,
      _BankAccountText
}
